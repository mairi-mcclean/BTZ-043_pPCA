
$PROB BTZ-043 CFU TTP and MBLA model code
$INPUT ID TIME DV FLAG EXPOSURE
$DATA ....csv


$PRED

;---------------code for exposure effect-----------------------------
EXPOSUREeff= 1+(EXPOSURE/16071)*THETA(8)

;----------------------------- PD model------------------------------
INTERCEPTCFU = THETA(1) *EXP(ETA(1))
INTERCEPTTTP = THETA(2) *EXP(ETA(2))
INTERCEPTMBLA = THETA(9) *EXP(ETA(3))

BETA1BTZCFU = THETA(3) *EXP(ETA(4))*EXPOSUREeff
BETA1MBLA = THETA(10) *EXP(ETA(6))*EXPOSUREeff
BETA1BTZTTP = THETA(5) *EXP(ETA(5))*EXPOSUREeff

BETA2BTZCFU = THETA(4) *EXP(ETA(7))
BETA2BTZTTP = THETA(6) *EXP(ETA(8))
BETA2MBLA = THETA(11) *EXP(ETA(9))

NODE = THETA(7)

;------------------switch between CFU TTP or MBLA----------------
IF(FLAG.EQ.1)THEN
BETA1=BETA1BTZCFU
BETA2=BETA2BTZCFU
ENDIF
IF(FLAG.EQ.2)THEN
BETA1=BETA1BTZTTP
BETA2=BETA2BTZTTP
ENDIF
IF(FLAG.EQ.3)THEN
BETA1=BETA1MBLA
BETA2=BETA2MBLA
ENDIF

;-----------------Calculate CFU TTP MBLA-------------------------
CFU1= INTERCEPTCFU - BETA1*TIME 
CFUatNODE= INTERCEPTCFU - BETA1*NODE
CFU2= CFUatNODE - BETA2*(TIME-NODE)

TTP1= INTERCEPTTTP + BETA1*TIME
TTPatNODE= INTERCEPTTTP + BETA1*NODE
TTP2= TTPatNODE + BETA2*(TIME-NODE)

MBLA1= INTERCEPTMBLA - BETA1*TIME 
MBLAatNODE= INTERCEPTMBLA - BETA1*NODE
MBLA2= MBLAatNODE - BETA2*(TIME-NODE)

CFU = CFU1
IF (TIME.GT.NODE)  CFU= CFU2 

TTP = TTP1
IF (TIME.GT.NODE)  TTP= TTP2 

MBLA = MBLA1
IF (TIME.GT.NODE)  MBLA= MBLA2 

IF(FLAG.EQ.1) IPRED = CFU 
IF(FLAG.EQ.2) IPRED = TTP 
IF(FLAG.EQ.3) IPRED = MBLA 

IF (FLAG.EQ.1) ADDERR = EPS(1)
IF (FLAG.EQ.2) ADDERR = EPS(2)
IF (FLAG.EQ.3) ADDERR = EPS(3)

Y=IPRED + ADDERR
IRES = DV - IPRED

$THETA
(0, 6.18) ;1 INTERCEPTCFUP1
(0, 1.99) ;2 INTERCEPTTTPP1
(0, 0.0052) ;3  B1CFU EMAX
(0, 0.0026) ;4 B2CFU
(0, 0.0008) ;5  B1TTP EMAX
(0, 0.0004) ;6 B2TTP
(0, 48) FIX ;7 NODE
(0, 1.01)  ; 8 lin ex-res
(0, 7.13) ;9 INTERCEPT MBLA
(0, 0.0036) ;10  B1MBLA
(0, 0.0029) ;11 B2MBLA

$OMEGA BLOCK(3)
 0.0254  ;1 INTECEPTCFU
 -0.0063 0.00266  ;2 INTECEPTTTP
 0.0149 -0.0056 0.014  ;3 INTERCEPT MBLA

$OMEGA BLOCK(3)
 0.269; slope 1 CFU
 0.178 0.348  ; slope1 TTP
 0.216 0.382 0.502  ;slope 1 MBLA

$OMEGA BLOCK(3)
 0.798;4 SLOPE2CFU
 0.482 0.473;5 SLOPE2TTP
 0.694 0.672 1.05;5 SLOPE2MBLA

$SIGMA
 0.228 ; add err CFU
 0.0031 ; add err TTP
 0.189 ; add err MBLA

$ESTIMATION METHOD=1 INTERACTION MAXEVAL=9999 NSIG=3 PRINT=1
$COV 

